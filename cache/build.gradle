plugins {
    id 'java-library'
    id 'antlr'
}

ext {
    antlrVersion = '4.6'
    cacheVersion = 165
}

dependencies {
    implementation project(':http-api')
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'org.apache.commons:commons-compress:1.10'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'io.netty:netty-buffer:4.1.0.Final'
    implementation 'commons-cli:commons-cli:1.3.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.slf4j:slf4j-simple:1.7.12'
    antlr "org.antlr:antlr4:${antlrVersion}"
}

processResources {
    filesNotMatching('djb2.properties') {
        expand(project.properties)
    }
}

processTestResources {
    expand(project.properties)
}

tasks.withType(Checkstyle) {
    exclude '**/rs2asmParser.java'
    exclude '**/rs2asmLexer.java'
    exclude '**/rs2asmBaseListener.java'
    exclude '**/rs2asmListener.java'
}

task testsJar(type: Jar) {
    archiveClassifier.set('tests')
    from(sourceSets.test.output)
}

description = 'Cache'
publishing.publications.maven.artifact(testsJar)
